/*
 * Copyright (c) 2012-2015, Microsoft Mobile
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: '../../shared.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        // Use snapshot repository for now, until released to Maven central public
        url uri('https://oss.sonatype.org/content/repositories/snapshots/')
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'com.intellij', name: 'annotations', version: '9.0.4'

    compile group: 'org.juniversal', name: 'juniversal-translator', version: masterVersion

    testCompile 'junit:junit:4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = 'org.juniversal'
version = masterVersion
archivesBaseName = "juniversal-gradle-plugins"

compileJava.targetCompatibility = "1.6"
compileJava.sourceCompatibility = "1.6"

jar {
    manifest {
        attributes 'Implementation-Title': "JUniversal translators and related tool Gradle plugins",
                'Implementation-Version': "${version} build ${buildNumber}",
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
    }
}

uploadArchives {
    repositories {
/*
        mavenDeployer {
            repository(url: uri('../repo'))
        }
*/

        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

/*
            repository(url: uri('../repo'))
*/

            if (project.hasProperty('ossrhUsernameJUniversal') && project.hasProperty('ossrhPasswordJUniversal')) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsernameJUniversal, password: ossrhPasswordJUniversal)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsernameJUniversal, password: ossrhPasswordJUniversal)
                }
            }

            pom.project {
                name 'JUniversal Gradle plugins'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Gradle plugin tasks here are for source code conversion and invoking MSBuild'
                url 'https://github.com/juniversal/juniversal'

                scm {
                    connection 'scm:git:git@github.com:juniversal/juniversal.git'
                    developerConnection 'scm:git:git@github.com:juniversal/juniversal.git'
                    url 'git@github.com:juniversal/juniversal.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        //id 'manfred'
                        name 'Bret Johnson'
                        email 'bret.johnson@microsoft.com'
                    }
                }
            }
        }
    }
}
